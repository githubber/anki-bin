#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;

my $db = Anki::Database->new(file => $ENV{ANKI_DECK});
my $sth = $db->prepare("
    select sentence.value, context.value, reading.value
    from fields as sentence
        join fields as reading on (sentence.factId = reading.factId)
        join fields as context on (sentence.factId = context.factId)
        join fieldModels as sentenceModel on (sentence.fieldModelId = sentenceModel.id)
        join fieldModels as readingModel  on (reading.fieldModelId  = readingModel.id)
        join fieldModels as contextModel  on (context.fieldModelId  = contextModel.id)
        join models on (sentenceModel.modelId = models.id)
    where
        models.name             = '文'
        and sentenceModel.name  = '日本語'
        and readingModel.name   = '読み'
        and contextModel.name   = '前後関係'
        and reading.value      <> '';
");

$sth->execute;

my $i = 0;
while (my ($sentence, $context, $reading_field) = $sth->fetchrow_array) {
    my @readings = map { /^([^【]+)/ } split /<br.*?>/, $reading_field;
    my @spurious;
    for my $kanji (grep defined, map { /(\p{Han}+)/g } @readings) {
        my $regex = join 'っ*', split '', $kanji;
        next if "$sentence$context" =~ $regex;
        push @spurious, $kanji;
    }
    next if !@spurious;

    say ++$i . ". " . $sentence;
    say "    $_" for @spurious;
}

__END__
real sample output:

誰と会ってみたいですか。
    合
もし時間が合ったらスカイプでも話そう！
    会
正規表現, 本っっ当に面白いですねぇ
    本当
記憶はつながってる
    暗記
次はいつ会いましょうか。
    合
今，大地震が起きたと仮定したらまずどうしますか
    大地獄
