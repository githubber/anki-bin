#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Encode 'decode_utf8';
use DBI;

my $dbh = DBI->connect("dbi:SQLite:dbname=$ENV{HOME}/Documents/Anki/Japanese.anki");
my $sth = $dbh->prepare("
    select sentence.value, reading.value
    from fields as sentence
        join fields as reading on (sentence.factId = reading.factId)
    where
        reading.fieldModelId=-988607031910086482
        and sentence.fieldModelId=4775683831775348908
        and reading.value<>'';
");

$sth->execute;

while (my ($sentence, $reading_field) = map { decode_utf8 $_ } $sth->fetchrow_array) {
    next if $reading_field !~ /【/; # weirdly formatted reading field

    my @readings = map { /^([^【]+)/ } split /<br.*?>/, $reading_field;
    my @spurious;
    for my $kanji (grep defined, map { /(\p{Han}+)/g } @readings) {
        my $regex = join 'っ*', split '', $kanji;
        next if $sentence =~ $regex;
        push @spurious, $kanji;
    }
    next if !@spurious;

    say $sentence;
    say "    $_" for @spurious;
}

__END__
real sample output:

誰と会ってみたいですか。
    合
もし時間が合ったらスカイプでも話そう！
    会
正規表現, 本っっ当に面白いですねぇ
    本当
記憶はつながってる
    暗記
次はいつ会いましょうか。
    合
今，大地震が起きたと仮定したらまずどうしますか
    大地獄
