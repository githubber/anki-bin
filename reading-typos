#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;

my %known_homographs = (
    '1日'    => [qw/いちにち  ついたち/],
    '娘'     => [qw/こ        むすめ/],
    '名'     => [qw/めい      な/],
    '後'     => [qw/あと      ご/],
    '我'     => [qw/われ      わ/],
    '下'     => [qw/した      もと/],
    '人'     => [qw/ひと      じん/],
    '何'     => [qw/なに      なん/],
    '側'     => [qw/かわ      そば/],
    '君'     => [qw/きみ      くん/],
    '城'     => [qw/しろ      じょ/],
    '大'     => [qw/だい      おお/],
    '孫'     => [qw/そん      まご/],
    '家'     => [qw/いえ      うち/],
    '山'     => [qw/やま      さん/],
    '方'     => [qw/かた      ほう/],
    '星'     => [qw/ほし      せい/],
    '的'     => [qw/てき      まと/],
    '空'     => [qw/から      そら/],
    '訳'     => [qw/やく      わけ/],
    '金'     => [qw/かね      きん/],
    '中'     => [qw/なか      じゅう/],
    '敵'     => [qw/てき      きてき/],
    '皆'     => [qw/みな      みんな/],
    '間'     => [qw/かん      あいだ/],
    '声'     => [qw/こえ      ヴォイス/],
    '超'     => [qw/ちょう    スーパー/],
    '吐く'   => [qw/つく      はく/],
    '空く'   => [qw/あく      すく/],
    '良い'   => [qw/いい      よい/],
    '行く'   => [qw/いく      ゆく/],
    '入る'   => [qw/いる      はいる/],
    '開く'   => [qw/あく      ひらく/],
    'お腹'   => [qw/おなか    おはら/],
    '何か'   => [qw/なにか    なんか/],
    '臭い'   => [qw/におい    くさい/],
    '辛い'   => [qw/からい    つらい/],
    '日本'   => [qw/にっぽん  にほん/],
    '一日'   => [qw/いちにち  ついたち/],
    '止める' => [qw/とめる    やめる/],
);
my %readings_of;

my $db = Anki::Database->new(file => $ENV{ANKI_DECK});
my $sth = $db->prepare("
    select fields.factId, fields.value
    from fields
        join fieldModels on (fields.fieldModelId = fieldModels.id)
        join models on (fieldModels.modelId = models.id)
    where
        models.name is '文'
        and fieldModels.name like '%読み%'
        and fields.value <> '';
");

$sth->execute;

while (my ($fid, $reading_field) = $sth->fetchrow_array) {
    my @readings = split /<br.*?>/, $reading_field;
    for (@readings) {
        if (my ($word, $reading) = /^([^【]+)【([^】]+)】$/) {
            $readings_of{$word}{$reading} = 1;
            next;
        }
        warn "$fid|$_\n";
    }
}

for my $word (sort keys %readings_of) {
    if ($known_homographs{$word}) {
        $readings_of{$word}{$_} = 0 for @{ $known_homographs{$word} };
    }

    # only show words with more than one reading
    # or with kanji in the reading itself (probably over-eagerly converted)
    # or with only kana in the word (probably forgot to convert)
    next if (grep { $_ } values %{ $readings_of{$word} }) <= 1
         && (join '', keys %{ $readings_of{$word} }) !~ /\p{Han}/
         && $word !~ /^(\p{Hiragana}|\p{Katakana})+$/;

    say "$word: " . join (', ', sort keys %{ $readings_of{$word} });
}

__END__
中: なか, なか？
今日: きょう, きよう
会社: かいしゃ, 会社
信じる: しんじる, 信じる
回復: かいふき, かいふく
変わる: かわる, くわる
存在: そんざい, ぞんさい
引数: ひきすう, ひくすう
恐怖: きょうふ, きょふ
月: つき, つき？
歌う: うたう, 歌う
水死: すいし, すいしたい
簡単: かんたん, たんたん
読み込む: よみこむ, 読み込む
遊ぶ: あそび, あそぶ
