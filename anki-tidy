#!/usr/bin/env perl
use 5.14.0;
use warnings;
use Anki::Database;
use utf8::all;

my $db = Anki::Database->new;
my $sth = $db->prepare('
    select fields.factId, fields.value from fields
    join fieldModels on fieldModels.id = fields.fieldModelId

    where

    -- fields with readings separated by \n not br - caused by AnkiMobile
    (
        fieldModels.name = "読み"
        and fields.value like "%】%【%"
        and fields.value not like "%<br%"
    )

    -- fields with leading or trailing whitespace
    or fields.value like " %" or fields.value like "% "

    -- fields with trailing 。
    or (fields.value like "%。" and fields.value not like "%。。")

    -- fields with smart quotes
    or fields.value like "%“%" or fields.value like "%”%"
;');

$sth->execute;

while (my ($fid, $sentence) = $sth->fetchrow_array) {
    $sentence =~ s/^(\s+)|(\s+)$|(。)$|(“)|(”)/\e[1;41m$+\e[m/g;
    say "$fid|$sentence";
}

