#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Morphology;

my %wanted = map { $_ => 1 } qw(
    映画
    読み物
    ゲーム
);
my %new_morph;
my %all_sentences;
my %seen_morpheme;

my $morph = Anki::Morphology->new;
my $db = $morph->anki;
my $sth = $db->prepare("
    select sentence.value, facts.tags
    from fields as sentence
        join fieldModels on (sentence.fieldModelId = fieldModels.id)
        join cards on (cards.factId = sentence.factId)
        join facts on (facts.id = sentence.factId)
    where
        fieldModels.name = '日本語'
        and cards.type > 0
    order by cards.firstAnswered asc
;");
$sth->execute;

while (my ($sentence, $tags) = $sth->fetchrow_array) {
    my @new;
    for my $morpheme ($morph->morphemes_of($sentence)) {
        push @new, $morpheme->{dictionary}
            if $seen_morpheme{$morpheme->{dictionary}}++ == 0;
    }

    for my $tag (grep { $wanted{$_} } split ' ', $tags) {
        push @{ $new_morph{$tag} }, @new;
        $all_sentences{$tag}++;
    }
}

for my $tag (keys %wanted) {
    say "$tag: " . @{ $new_morph{$tag} } . " new, $all_sentences{$tag} all";
}
