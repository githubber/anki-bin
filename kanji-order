#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;
use Lingua::JA::Heisig 'heisig_number', 'kanji';

my $db = Anki::Database->new;
my $kanji_sth = $db->prepare("
    select kanji.value, models.name, facts.tags
    from fields as kanji
        join fieldModels as kanjiField on (kanji.fieldModelId = kanjiField.id)
        join cards on (cards.factId = kanji.factId)
        join cardModels on (cards.cardModelId = cardModels.id)
        join facts on (cards.factId = facts.id)
        join models on (facts.modelId = models.id)
    where
        kanjiField.name = '漢字'
        and cardModels.name = '書け'
        and facts.tags not like '%duplicate-kanji%'
        order by cards.created asc
;");

$kanji_sth->execute;
my $expected = 1;
while (my ($kanji, $model, $tags) = $kanji_sth->fetchrow_array) {
    my $heisig = heisig_number($kanji);

    # right model?
    if ($heisig && $model eq '漢字') {
        warn "Heisig kanji H$heisig $kanji does not use the Heisig 漢字 model.\n";
    }
    if (!$heisig && $model eq 'Heisig 漢字') {
        warn "Nonheisig kanji $kanji uses the Heisig 漢字 model.\n";
    }

    if ((!$heisig || $heisig > 2042) && $tags =~ /rtk1/) {
        warn "$kanji (H$heisig): spurious rtk1 tag";
    }

    if ((!$heisig || $heisig <= 2042 || $heisig >= 3007) && $tags =~ /rtk3/) {
        warn "$kanji (H$heisig): spurious rtk3 tag";
    }

    if ((!$heisig || $heisig < 3008) && $tags =~ /rtks/) {
        warn "$kanji (H$heisig): spurious rtks tag";
    }

    # heisig kanji in the right order?
    next if !$heisig;
    if ($heisig != $expected) {
        my $ex_kanji = (kanji)[$expected-1];
        warn "H$expected: $kanji (H$heisig) came too soon! Expected $ex_kanji!\n";
        $expected--;
    }

    if ($heisig < 2042 && $tags !~ /rtk1/) {
        warn "$kanji (H$heisig): missing rtk1 tag";
    }
    if (($heisig > 2042 && $heisig <= 3007) && $tags !~ /rtk3/) {
        warn "$kanji (H$heisig): missing rtk3 tag";
    }
    if (($heisig > 3007 && $heisig < 3030) && $tags !~ /rtks/) {
        warn "$kanji (H$heisig): missing rtks tag";
    }

    $expected++;
}

