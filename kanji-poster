#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Encode 'decode_utf8';
use DBI;

my $dbh = DBI->connect("dbi:SQLite:dbname=$ENV{ANKI_DECK}");
my $sth = $dbh->prepare("
    select kanji.value, reading.value
    from fields as kanji
        join fieldModels as kanjiField on (kanji.fieldModelId = kanjiField.id)
        join fields as reading on (kanji.factId = reading.factId)
        join fieldModels as readingField on (reading.fieldModelId = readingField.id)
        join cards on (cards.factId = kanji.factId)
        join cardModels on (cards.cardModelId = cardModels.id)
        join models on (kanjiField.modelId = models.id)
    where
        kanjiField.name = '漢字'
        and readingField.name = '読み'
        and models.name = 'Heisig 漢字'
        and cardModels.name = '書け'
        order by cards.created asc
;");

$sth->execute;

my @kanji;
while (my ($kanji, $reading) = map { decode_utf8($_) } $sth->fetchrow_array) {
    my $state = 'learned';

    $state = 'reading' if length $reading;
    push @kanji, [$kanji, $state];
}

my $i = 0;
for (@kanji) {
    my ($kanji, $state) = @$_;

    given ($state) {
        when ('reading') { print "\e[1;31m" }
    }

    print $kanji;

    print "\e[m" unless $state eq 'learned';

    print "\n" if ++$i % 32 == 0;
}

