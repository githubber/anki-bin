#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;
use List::Util 'first';

my %is_allowed = map { $_ => 1 } qw(
    ゲーム
    カラオケ
    読み物
    映画

    日本地図
    東京地図

    自愛
    早口言葉

    rtk1
    rtk3
    rtks

    Leech
    context-only
    from-corpus
    duplicate-kanji
    atomic-prompt
);
my %seen;

my $db = Anki::Database->new;
my $sth = $db->prepare("
    select facts.id, cards.tags, facts.tags
    from cards
    join facts on (facts.id = cards.factId)
    where
        cards.tags <> ''
        or facts.tags <> ''
;");

$sth->execute;

while (my ($fid, $c_tags, $f_tags) = $sth->fetchrow_array) {
    my @tags = ((split ' ', $c_tags), (split ' ', $f_tags));
    for my $tag (@tags) {
        $seen{$tag}++;
        if (!$is_allowed{$tag}) {
            say "$fid|$tag";
        }
    }
}

for my $tag (keys %is_allowed) {
    if (!$seen{$tag}) {
        warn "allowed tag has no cards: $tag\n";
    }
}
