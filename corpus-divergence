#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;
use Anki::Corpus;

my $japanese_db = Anki::Database->new;
my $sth = $japanese_db->prepare("
    select sentence.value, source.value
    from fields as sentence
        join fieldModels as sentenceFM on (sentence.fieldModelId = sentenceFM.id)
        join fields as source on (sentence.factId = source.factId)
        join fieldModels as sourceFM on (source.fieldModelId = sourceFM.id)
        join models on (sentenceFM.modelId = models.id)
    where
        models.name = '文'
        and sentenceFM.name = '日本語'
        and sourceFM.name = '出所'
;");
$sth->execute;
my %anki_has = map { $_->[0] => $_->[1] } @{ $sth->fetchall_arrayref };

$sth = $japanese_db->prepare("
    select yoji.value
    from fields as yoji
        join fieldModels on (yoji.fieldModelId = fieldModels.id)
    where
        fieldModels.name = '四字熟語'
;");
$sth->execute;
$anki_has{$_->[0]} = '四字熟語' for @{ $sth->fetchall_arrayref };

my $corpus = Anki::Corpus->new;

my @bad_source;

my $printed_header = 0;
$corpus->print_each("WHERE suspended = 0", sub {
    my $sentence = shift;
    if (my $anki_source = $anki_has{$sentence->japanese}) {
        if ($anki_source ne $sentence->source) {
            push @bad_source, [$sentence->japanese, $anki_source, $sentence->source, $sentence->id]
                unless ($anki_source =~ /twitter.com|^@/ && $sentence->source eq 'Twitter');
        }
        return 0;
    }
    say "=== MISSING FROM ANKI (add to anki) ===" unless $printed_header++;
    return 1;
});

if (@bad_source) {
    say "=== INCORRECT SOURCE (fix anki or delete from corpus) ===";
    for (@bad_source) {
        my ($sentence, $anki, $corpus, $id) = @$_;
        say "$id: $sentence";
        say "    expected: $corpus";
        say "         got: $anki";
        say "";
    }
}

$printed_header = 0;

$corpus->print_each("WHERE suspended = 1", sub {
    my $sentence = shift;
    return 0 if !$anki_has{$sentence->japanese};
    say "=== UNEXPECTEDLY IN ANKI (unsuspend in corpus or delete from anki) ===" unless $printed_header++;
    return 1;
});

