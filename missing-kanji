#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;
use Lingua::JA::Heisig 'heisig_number', 'kanji';

my $kanji_sql = join ', ', map { "'$_'" } kanji;

my $db = Anki::Database->new;
my $kanji_sth = $db->prepare("
    select kanji.value
    from fields as kanji
        join fieldModels as kanjiField on (kanji.fieldModelId = kanjiField.id)
        join cards on (cards.factId = kanji.factId)
        join cardModels on (cards.cardModelId = cardModels.id)
    where
        kanjiField.name = '漢字'
        and cardModels.name = '書け'
        and kanji.value not in ($kanji_sql)
;");

my %gaiji = (
    '々' => 1,
    '〇' => 1,
);

$kanji_sth->execute;
while (my ($kanji) = $kanji_sth->fetchrow_array) {
    $gaiji{$kanji}++;
}

my $sentence_sth = $db->prepare("
    select sentence.value
    from fields as sentence
        join fieldModels as sentenceFM on (sentence.fieldModelId = sentenceFM.id)
        join models on (sentenceFM.modelId = models.id)
        join cards on (sentence.factId = cards.factId)
    where
        models.name = '文'
        and sentenceFM.name = '日本語'
        and cards.type > 0
;");
$sentence_sth->execute;

while (my ($sentence) = $sentence_sth->fetchrow_array) {
    for my $kanji ($sentence =~ /\p{Han}/g) {
        next if heisig_number($kanji)
             || $gaiji{$kanji};

        warn "$kanji: $sentence\n";
        $gaiji{$kanji}++;
    }
}
