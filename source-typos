#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;
use List::Util 'first';

my %known_sources;
my @regex_sources;
my %type_of;

my %sources = (
    games => [
        'jNetHack',
        'クロノ・トリガー',
        'ブレス オブ ファイア 竜の戦士',
        'ブレス オブ ファイア III',
        'マックスウェルの不思議なノート',
        'ポケットモンスター ファイアレッド',
        'ファンタシースター 千年紀の終りに',
        'ファンタシースターII 還らざる時の終わりに',
        'Halo Reach',
        qr/^ファイナルファンタジー[IVX]+$/,
        'ファイナルファンタジータクティクス 獅子戦争',
        'ファイナルファンタジーUSA ミスティッククエスト',
        'ゼルダの伝説 大地の汽笛',
        'ゴッドハンド',
        '大江戸タウンズ',
        'MOTHER2 ギーグの逆襲',
        'バハムート ラグーン',
        'ドラゴンクエストIX 星空の守り人',
        'エピックハーツ',
        'もじとも',
        '大きな字の漢字ナンクロ',
    ],
    novels => [
        qr/^デューン砂の惑星[1-4]$/,
        'ハリー・ポッターと賢者の石',
        'ハリー・ポッターと秘密の部屋',
        'ハリー・ポッターとアズカバンの囚人',
        'ハリー・ポッターと炎のゴブレット',
        'ハリー・ポッターと不死鳥の騎士団',
        'ハリー・ポッターと謎のプリンス',
        'ハリー・ポッターと死の秘宝',
        'ホビットの冒険',
        '指輪物語',
        '坑夫',
        '吾輩は猫である',
        'こゝろ',
        '坊っちゃん',
    ],
    manga => [
        qr/^ドラゴンボール\d+$/,
        qr/^北斗の拳\d+$/,
        qr/^天才バカボン\d+$/,
        qr/^ヒカルの碁\d+$/,
        qr/^バガボンド\d+$/,
    ],
    movies => [
        'マトリックス',
        'マトリックス リローデッド',
        'マトリックス レボリューションズ',
        'ブレイド',
        'ブレイド2',
        'アキラ',
        'Die Hard 2',
        qr{^ロード・オブ・ザ・リング/(旅の仲間|二つの塔|王の帰還)$},
        "Ocean's 11",
        'Constantine',
        'Babel',
        'ハリー・ポッターと賢者の石(映画)',
        'ゴールデンアイ',
        '乱',
        'グラディエーター',
        '用心棒',
        'Queen of the Damned',
        'The Simpsons Movie',
        '魔女の宅急便',
        'ボーン・スプレマシー',
        'キル・ビル',
        'トロイ',
    ],
    television => [
        qr/^ドラゴンボール改\d+$/,
        qr/^北斗の拳\d+話目$/,
        'あやかの突撃英会話',
        'ドラゴンボール改',
        'ロス・タイム・ライフ',
    ],
    tools => [
        'プログレッシブ英和・和英中辞典',
        'Tae Kim',
        'Smart.fm',
        'MFSP',
        'Making Out in Japanese',
        'Dirty Guide to Japanese',
        'Genki',
        'A Dictionary of Japanese Particles',
        'ドラえもん四字熟語100',
        '大辞泉',
        '大辞林',
        'ドラえもんのまんがで英語辞典覚える',
        'goo',
        'ARES-3',
    ],
    references => [
        'モダンPerl入門',
        'CPANモジュールガイド',
        qr/^WEB\+DB Press: .+/,
    ],
    songs => [
        qr/^Mr\. Children - .*$/,
        qr/^L'Arc~en~Ciel - .*$/,
        qr/^Gackt - .*$/,
        qr/^高田雅史 - .*$/,
        qr/^分島花音 - .*$/,
        qr/^Malice Mizer - .*$/,
        qr/^Perfume - .*$/,
        qr/^浜崎あゆみ - .*$/,
        qr/^Pizzicato Five - .*$/,
        qr/^Rumi - .*$/,
    ],
    apps => [
        'RT',
        'Jifty',
        'Facebook',
        'OS X',
        'Gmail',
        'Echofon',
        'YouTube',
        'Twitter',
        'Foursquare',
        'Google Reader',
        'Firefox',
        'Skype',
        'NetNewsWire',
        'Last.fm',
        'iOS',
        'Japanese.app',
        'Amazon',
        'Anki',
        'ニコニコ動画',
        'Siri',
    ],
    podcasts => [
        '読売ニュースポッドキャスト',
        'モヤモヤとーく',
        'ヒデラジ',
    ],
    conversations => [
        qr/^Personal correspondence with .+/,
        qr/^@\w+$/,
    ],
    real_life => [
        '中村先生',
        '高橋先生',
        '公共交通機関',
        qr/^(Sign|Announcement|Conversation|Pamphlet) (at|in|on|with|from) .+/,
        'YAPC::Asia',
        '広告',
    ],
);

my %expected_tags = (
    games      => 'ゲーム',
    songs      => 'カラオケ',
    novels     => '読み物',
    manga      => '読み物',
    references => '読み物',
    movies     => '映画',
);

my %reverse_tags;
for my $type (keys %expected_tags) {
    my $tag = $expected_tags{$type};
    push @{ $reverse_tags{$tag} }, $type;
}

for my $type (keys %sources) {
    for my $source (@{ $sources{$type} }) {
        $type_of{$source} = $type;

        if (ref($source) eq 'Regexp') {
            push @regex_sources, $source;
        }
        else {
            $known_sources{$source} = $source;
        }
    }
}

sub type_of {
    my $source = shift;
    my $source_template = $known_sources{$source} || first { $source =~ $_ } @regex_sources;
    return if !$source_template;

    my $type = $type_of{$source_template};
    die "$source_template has no type ??\n" if !$type;

    return $type;
}

my $db = Anki::Database->new;
my $sth = $db->prepare("
    select fields.value, fields.factId, facts.tags
    from fields
        join fieldModels on (fields.fieldModelId = fieldModels.id)
        join models on (fieldModels.modelId = models.id)
        join facts on (fields.factId = facts.id)
    where
        models.name is '文'
        and fieldModels.name like '%出所%'
        and fields.value not like 'http%'
        and fields.value not like '# %'
;");

$sth->execute;

my %count;

while (my ($source, $factid, $tags) = $sth->fetchrow_array) {
    if (my $type = type_of($source)) {
        if ($expected_tags{$type} && $tags !~ $expected_tags{$type}) {
            warn "$factid|$source - didn't include tag $expected_tags{$type} expected of $type\n" unless $tags =~ /from-corpus/;
        }
        next;
    }

    $count{$source}++;
}

for my $source (sort { $count{$b} <=> $count{$a} } keys %count) {
    say "$count{$source}\t$source";
    say "\t\tYou meant プログレッシブ英和・和英中辞典" if $source eq 'Dictionary.app';
}

$sth = $db->prepare("
    select fields.value, fields.factId
    from fields
        join fieldModels on (fields.fieldModelId = fieldModels.id)
        join models on (fieldModels.modelId = models.id)
    where
        models.name is '文'
        and fieldModels.name like '%出所%'
;");

$sth->execute;

while (my ($source, $factid) = $sth->fetchrow_array) {
    say "$factid|$source" if $source =~ /\n|<br/;
    say $source if $source =~ m{(twitter|facebook)\.com/\#\!};
}

for my $tag (keys %reverse_tags) {
    my @types = @{ $reverse_tags{$tag} };

    my $sth = $db->prepare("
        select fields.value, fields.factId
        from fields
            join fieldModels on (fields.fieldModelId = fieldModels.id)
            join models on (fieldModels.modelId = models.id)
            join facts on (fields.factId = facts.id)
        where
            models.name is '文'
            and facts.tags like ?
            and fieldModels.name like '%出所%'
            and fields.value not like 'http%'
            and fields.value not like '# %'
    ;");

    $sth->execute("%$tag%");

    while (my ($source, $factid) = $sth->fetchrow_array) {
        my $type = type_of($source);
        unless ($type && grep { $type eq $_ } @types) {
            say "$factid|$source has spurious $tag tag";
        }
    }
}
