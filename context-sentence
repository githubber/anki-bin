#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;
use List::Util 'first';

my $db = Anki::Database->new(file => $ENV{ANKI_DECK});
my $sth = $db->prepare(q{
    select context.factId, context.value
    from fields as context
    join fieldModels on (context.fieldModelId = fieldModels.id)
    join facts on (context.factId = facts.id)
    where
        fieldModels.name = '前後関係'
        and context.value <> ''
        and facts.tags not like '%context-only%'
;});

$sth->execute;

my $sentence_sth = $db->prepare(q{
    select count(sentence.value)
    from fields as sentence
        join fieldModels on (sentence.fieldModelId = fieldModels.id)
        join models on (fieldModels.modelId = models.id)
    where
        models.name = '文'
        and fieldModels.name = '日本語'
        and sentence.value like ?
;});

my %seen;

while (my ($fid, $context_field) = $sth->fetchrow_array) {
    for my $context (split /<br.*?>/, $context_field) {
        next if $context !~ /\p{Han}|\p{Hiragana}|\p{Katakana}/;
        next if $seen{$context}++;

        $sentence_sth->execute($context);
        my ($count) = $sentence_sth->fetchrow_array;
        next if $count;

        say "$fid|$context";
    }
}

