#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;
use Anki::Corpus;
use List::MoreUtils 'all';

my $ankidir = "$ENV{HOME}/Documents/Anki";

my $japanese_db = Anki::Database->new;
my $corpus = Anki::Corpus->new;

my %seen_kanji = map { $_ => 1 } my_kanji();
my %seen_yoji  = map { $_ => 1 } my_yoji();

my @learnable = grep { !$seen_yoji{$_->[0]} } learnable_yoji(\%seen_kanji);

my $prev_freq = '';
for (@learnable) {
    my ($yoji, $reading, $meaning, $freq) = @$_;
    say "\n" . ($prev_freq = $freq) . ":" if $freq ne $prev_freq;
    say "    $yoji【$reading】$meaning";
}

sub my_kanji {
    my $sth = $japanese_db->prepare("
        select kanji.value
            from fields as kanji
                join fieldModels as kanjiFieldModel on (kanji.fieldModelId = kanjiFieldModel.id)
                join fields as reading on (kanji.factId = reading.factId)
                join fieldModels as readingFieldModel on (reading.fieldModelId = readingFieldModel.id)
                join facts on (kanji.factId = facts.id)
            where
                kanjiFieldModel.name = '漢字'
                and readingFieldModel.name = '読み'
                and reading.value <> ''
                order by facts.created
    ;");

    $sth->execute;

    my @kanji;
    while (my ($kanji) = $sth->fetchrow_array) {
        push @kanji, $kanji;
    }
    return @kanji;
}

sub my_yoji {
    my $sth = $japanese_db->prepare("
        select yoji.value
            from fields as yoji
            join fieldModels on (yoji.fieldModelId = fieldModels.id)
            join cards as yoji_card on (yoji.factId = yoji_card.factId)
        where
            fieldModels.name = '四字熟語'
            and yoji_card.type >= 0
    ;");

    $sth->execute;

    my @yoji;
    while (my ($yoji) = $sth->fetchrow_array) {
        push @yoji, $yoji;
    }
    return @yoji;
}

sub learnable_yoji {
    my $kanji = shift;

    my $sth = $corpus->prepare("
        select sentences.japanese, sentences.readings, sentences.translation, frequency.value
        from sentences
        join notes as frequency on (frequency.sentence = sentences.rowid)
        where source='四字熟語'
        and frequency.type = '使用頻度'
        order by frequency.value
    ;");

    $sth->execute;

    my @yoji;
    while (my ($yoji, $reading, $meaning, $frequency) = $sth->fetchrow_array) {
        push @yoji, [$yoji, $reading, $meaning, $frequency]
            if all { $kanji->{$_} } split '', $yoji;
    }
    return @yoji;
}
