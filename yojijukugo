#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Encode 'decode_utf8';
use DBI;
use List::MoreUtils 'all';

my $ankidir = "$ENV{HOME}/Documents/Anki";
my $yoji_deck = "$ankidir/yojijukugo.anki";

my $japanese_dbh = DBI->connect("dbi:SQLite:dbname=$ENV{ANKI_DECK}");
my $yoji_dbh = DBI->connect("dbi:SQLite:dbname=$yoji_deck");

my %seen_kanji = map { $_ => 1 } my_kanji();
my %seen_yoji  = map { $_ => 1 } my_yoji();

my @learnable = grep { !$seen_yoji{$_->[0]} } learnable_yoji(\%seen_kanji);

my $prev_freq = '';
for (@learnable) {
    my ($yoji, $reading, $meaning, $freq) = @$_;
    say "\n" . ($prev_freq = $freq) . ":" if $freq ne $prev_freq;
    say "    $yoji【$reading】$meaning";
}

sub my_kanji {
    my $sth = $japanese_dbh->prepare("
        select kanji.value
        from fields as kanji
        join fields as reading on (kanji.factId = reading.factId)
        join facts as kanji_fact on (kanji_fact.id = kanji.factId)
        where (
               kanji.fieldModelId=7694462791390410032 -- heisig
            or kanji.fieldModelId=808916364535512610  -- non-heisig
        )
        and (
               reading.fieldModelId=1870991842743010604  -- heisig
            or reading.fieldModelId=-2684691650435204645 -- non-heisig
        )
        and reading.value <> ''
        order by kanji_fact.created
    ;");

    $sth->execute;

    my @kanji;
    while (my ($kanji) = map { decode_utf8 $_ } $sth->fetchrow_array) {
        push @kanji, $kanji;
    }
    return @kanji;
}

sub my_yoji {
    my $sth = $japanese_dbh->prepare("
        select yoji.value
        from fields as yoji
        join cards as yoji_card on (yoji.factId = yoji_card.factId)
        where yoji.fieldModelId=7140405698650152527
        and yoji_card.type>0
    ;");

    $sth->execute;

    my @yoji;
    while (my ($yoji) = map { decode_utf8 $_ } $sth->fetchrow_array) {
        push @yoji, $yoji;
    }
    return @yoji;
}

sub learnable_yoji {
    my $kanji = shift;

    my $sth = $yoji_dbh->prepare("
        select yoji.value, reading.value, meaning.value, frequency.value
        from fields as yoji
        join fields as frequency on (yoji.factId = frequency.factId)
        join fields as reading on (yoji.factId = reading.factid)
        join fields as meaning on (yoji.factId = meaning.factid)
        where yoji.fieldModelId=-507262820891124904
        and frequency.fieldModelId=-407147889135299611
        and reading.fieldModelId=167494270935804676
        and meaning.fieldModelId=-6330489706848322249
        order by frequency.value
    ;");

    $sth->execute;

    my @yoji;
    while (my ($yoji, $reading, $meaning, $frequency) = map { decode_utf8 $_ } $sth->fetchrow_array) {
        push @yoji, [$yoji, $reading, $meaning, $frequency]
            if all { $kanji->{$_} } split '', $yoji;
    }
    return @yoji;
}
