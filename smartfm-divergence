#!/usr/bin/env perl
use 5.14.0;
use warnings;
use utf8::all;
use Anki::Database;
use List::MoreUtils 'all';
use Array::Diff;

my $ankidir = "$ENV{HOME}/Documents/Anki";
my $smart_deck = "$ankidir/core2kcore6k.anki";

my $japanese_db = Anki::Database->new(file => $ENV{ANKI_DECK});
my $smart_db = Anki::Database->new(file => $smart_deck);

my $sth = $japanese_db->prepare("
    select sentence.value
    from fields as sentence
        join fields as source on (source.factId = sentence.factId)
        join fieldModels as sentenceModel on (sentence.fieldModelId = sentenceModel.id)
        join fieldModels as sourceModel on (source.fieldModelId = sourceModel.id)
    where
        sentenceModel.name = '日本語'
        and sourceModel.name = '起こり'
        and source.value = 'Smart.fm'
;");
$sth->execute;
my @japanese = sort map { $_->[0] } @{ $sth->fetchall_arrayref };

$sth = $smart_db->prepare("
    select sentence.value
    from fields as sentence
        join fieldModels on (sentence.fieldModelId = fieldModels.id)
        join cards on (sentence.factId = cards.factId)
    where
        fieldModels.name = 'Expression'
        and cards.type > 0
;");
$sth->execute;
my @smart = sort map { $_->[0] } @{ $sth->fetchall_arrayref };

my $diff = Array::Diff->diff(\@smart, \@japanese);
say "+$_" for @{ $diff->added };
say "-$_" for @{ $diff->deleted };

